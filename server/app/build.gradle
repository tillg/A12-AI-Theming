plugins {
    id 'java'
    alias(gradlePluginLibs.plugins.spring.boot)
    alias(gradlePluginLibs.plugins.test.logger)
    alias(gradlePluginLibs.plugins.rewrite)
}

ext {
    serviceName = rootProject.serverName
    imageLabels = ["name"       : "${calculateShortTag(rootProject.serverName)}".toString(),
                   "vendor"     : "mgm technology partners GmbH",
                   "version"    : project.version,
                   "release"    : rootProject.a12ReleaseVersion,
                   "summary"    : "A Data Services extensible server application that provides all functionalities to facilitate A12 Application",
                   "description": "A Data Services server with API and implementations to deal with models and documents supporting creation, retrieval, update, deletion and querying"]
}

configurations {
    runTypedAccessorGeneratorConfig
}

def generatedTypingsOutputDir = layout.buildDirectory.dir("generatedTypings").get().asFile

sourceSets {
    generatedTypings {
        java {
            srcDir(generatedTypingsOutputDir)
        }
    }
    main {
        java {
            compileClasspath += sourceSets.generatedTypings.output
            runtimeClasspath += sourceSets.generatedTypings.output
        }
    }
}

dependencies {
    //Open rewrite analysis
    rewrite libs.rewrite.static.analysis

    // Spring
    annotationProcessor libs.spring.boot.configuration.processor
    developmentOnly(libs.spring.boot.devtools)

    // A12 Dataservices
    implementation a12Libs.dataservices.server.app
    implementation(a12Libs.dataservices.data.seed)

    // A12 UAA
    implementation a12Libs.uaa.authorization.a12.extension
    implementation a12Libs.uaa.authorization.introspector

    // Native BOMs
    implementation(platform(a12Libs.base.bom))
    implementation(platform(a12Libs.kernel.bom))
    implementation(platform(a12Libs.uaa.bom))
    implementation(platform(a12Libs.dataservices.bom))

    // for logback.xml configurations
	//implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    implementation(libs.logstash)
    implementation(libs.logback.classic)

    runTypedAccessorGeneratorConfig group: 'com.mgmtp.a12.kernel', name: 'kernel-md-typed-accessor-gen', version: a12Libs.versions.kernel.get(), classifier: 'TypedAccessorGenerator-CLI'
    generatedTypingsImplementation group: 'com.mgmtp.a12.kernel', name: 'kernel-md-document-v2', version: a12Libs.versions.kernel.get()
    implementation group: 'com.mgmtp.a12.kernel', name: 'kernel-md-util', version: a12Libs.versions.kernel.get()
    implementation group: 'com.mgmtp.a12.kernel', name: 'kernel-md-facade', version: a12Libs.versions.kernel.get()
}


tasks.register('runTypedAccessorGenerator', JavaExec) {
    classpath = configurations.runTypedAccessorGeneratorConfig
    mainClass = 'com.mgmtp.a12.kernel.md.document.typed_accessor_gen.TypedAccessorGenerator'

    def modelPath = "${rootProject.projectDir.path}/import/models";
    def packagePrefix = "com.grtnr.ai_theming.server.typings"
    def documentModels = fileTree(dir: "${modelPath}", include: '**/*_DM.json')

    if (!documentModels || documentModels.isEmpty()) {
        throw new GradleException("No model definition files found in ${modelPath}")
    }
    def argsList = []
    argsList += ['--document-model-file', documentModels.first().absolutePath]
    argsList += ['--package-prefix', packagePrefix]
    argsList += ['--output-dir', generatedTypingsOutputDir]
    documentModels.each { File jsonFile ->
        argsList += ['-i', jsonFile.absolutePath]
    }
    args argsList
}

tasks.compileGeneratedTypingsJava.dependsOn(runTypedAccessorGenerator)

springBoot {
    mainClass = 'com.grtnr.ai_theming.server.AiThemingServerApplication'
    buildInfo {
        properties {
            version = project.version
        }
    }
}

artifacts artifactsVar

publishing publishingVar

rewrite rewriteVar
